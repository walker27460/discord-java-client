plugins {
	id 'java-library'
	id 'eclipse'
	id 'maven-publish'
}

java {
	withJavadocJar()
	withSourcesJar()

	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

javadoc.options.encoding = 'UTF-8'
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"


repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'junit:junit:4.13.2'

	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'com.google.code.gson:gson:2.10'

	// lombok
	compileOnly 'org.projectlombok:lombok:1.18.26'
	annotationProcessor 'org.projectlombok:lombok:1.18.26'

	testCompileOnly 'org.projectlombok:lombok:1.18.26'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
	// log
	implementation 'ch.qos.logback:logback-classic:1.3.5'
}

def versionNo = System.getenv('VERSION_NO') != null ? System.getenv('VERSION_NO') : '0'

publishing{
	repositories{
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/kurages/discord-java-client")
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications{
		gpr(MavenPublication){
			groupId = 'jp.kurages'
			artifactId = 'discord'
			version = "0.0.${versionNo}"
			from(components.java)
		}
	}
}
